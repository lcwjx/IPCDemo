apply plugin: 'checkstyle'

checkstyle {
  toolVersion '8.17'
  configFile file("../IPCDemo/config/checkstyle.xml")
  ignoreFailures false
  showViolations true
}

task checkstyle(type: Checkstyle) {

  def forceCopy = false
  //如有需要，可以强制去更新客户端的pre-commit文件
  try {
    copyPreCommitFile(forceCopy)
  } catch (Exception e) {
    println(e)
  }

  source './app/src/main/java'
  source './app_client/src/main/java'

  exclude '**/gen/**'
  exclude '**/R.java'
  exclude '**/BuildConfig.java'

  if (project.hasProperty('checkCommit') && project.property("checkCommit")) {
    def ft = filterCommitter(getChangeFiles())
    def includeList = new ArrayList<String>()
    for (int i = 0; i < ft.size(); i++) {
      String spliter = ft.getAt(i)
      String[] spliterlist = spliter.split("/")
      String fileName = spliterlist[spliterlist.length - 1]
      logger.debug("Checkstyle:file=" + fileName)
      includeList.add("**/" + fileName)
    }
    if (includeList.size() == 0) {
      exclude '**/*.java'
    } else {
      include includeList
    }
  } else {
    include '**/*.java'
  }
  classpath = files()
}

afterEvaluate {
  if (project.tasks.findByName('check')) {
    check.dependsOn('checkstyle')
  }
}

def filterCommitter(String gitstatusinfo) {
  ArrayList<String> filterList = new ArrayList<String>()
  String[] lines = gitstatusinfo.split("\\n")
  for (String line : lines) {
    if (line.contains(".java")) {
      String[] spliters = line.trim().split(" ")
      for (String str : spliters) {
        if (str.contains(".java")) {
          filterList.add(str)
        }
      }
    }
  }
  return filterList
}

def getChangeFiles() {
  try {
    String changeInfo = 'git status -s'.execute(null, project.rootDir).text.trim()
    return changeInfo == null ? "" : changeInfo
  } catch (Exception e) {
    return ""
  }
}

//把根目录下的pre-commit文件复制到.git-->hooks目录
def copyPreCommitFile(boolean forceUpdate) {

  def src = "${project.rootDir}/config/pre-commit"
  def target = "${project.rootDir}/.git/hooks"
  copyFile(forceUpdate, src, target)

  println("copyPreCommitFile")
}
